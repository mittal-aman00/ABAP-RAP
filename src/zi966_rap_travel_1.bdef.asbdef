managed;
with draft;
//strict ( 2 );

define behavior for ZI966_RAP_Travel_1 alias Travel
implementation in class zbp_i966_rap_travel_1 unique
persistent table zt966_rap_travel
draft table z966_rap_dtravel
lock master total etag LastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt
{
  create;
  update;
  delete;
  association _Booking { create; with draft; }

  field ( numbering : managed, readonly ) TravelUUID;
  field ( readonly ) TravelID , TotalPrice, TravelStatus;
  field ( readonly ) CreatedAt , CreatedBy , LastChangedAt , LastChangedBy , LocalLastChangedAt;
  field ( mandatory ) AgencyID , CustomerID;

  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;
  internal action recalcTotalPrice;

  determination setInitialStatus on modify { create; }
  determination calculateTotalPrice on modify { field BookingFee , CurrencyCode; }
  determination calculateTravelID on save { create; }

  validation validateAgency on save { field AgencyID; create; }
  validation validateCustomer on save { field CustomerID; create; }
  validation validateDates on save { field BeginDate , EndDate; create; }

    draft determine action Prepare  {
    validation validateAgency;
    validation validateCustomer;
    validation validateDates;
  }

  mapping for zt966_rap_travel
  {
    TravelUUID = travel_uuid;
    TravelID = travel_id;
    AgencyID = agency_id;
    CustomerID = customer_id;
    BeginDate = begin_date;
    EndDate = end_date;
    BookingFee = booking_fee;
    TotalPrice = total_price;
    CurrencyCode = currency_code;
    Description = description;
    TravelStatus = overall_status;
    CreatedBy = created_by;
    CreatedAt = created_at;
    LastChangedBy = last_changed_by;
    LastChangedAt = last_changed_at;
    LocalLastChangedAt = local_last_changed_at;
  }
}

define behavior for ZI966_RAP_Booking_1 alias Booking
implementation in class zbp_i966_rap_booking_1 unique
persistent table zt966_rap_bookin
draft table z966_rap_dbook
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChnagedAt
{
  association _Travel { with draft; }
  update;
  delete;



  //For the Booking entity, also add static field-control to the field
  //BookingID by setting it to readonly.
  //Do the same for the administrative fields CreatedBy, LastchangedBy and LocalLastChangedBy
  field ( numbering : managed, readonly ) BookingUUID;
  field ( readonly ) TravelUUID, BookingID;
  field ( readonly ) CreatedBy, LastChangedBy, LocalLastChnagedAt;

  //The calculateBookingID determination for determining the BookingID.
  //For updating the TotalPrice we add the calculateTotalPrice that is
  //triggered whenever the FlightPrice or CurrencyCode is changed.
  determination calculateBookingID  on modify { create; }
  determination calculateTotalPrice on modify { field FlightPrice, CurrencyCode; }

  mapping for zt966_rap_bookin
  {
    BookingUUID = booking_uuid;
    TravelUUID = travel_uuid;
    BookingID = booking_id;
    BookingDate = booking_date;
    CustomerID = customer_id;
    CarrierID = carrier_id;
    ConnectionID = connection_id;
    FlightDate = flight_date;
    FlightPrice = flight_price;
    CurrencyCode = currency_code;
    CreatedBy = created_by;
    LastChangedBy = last_changed_by;
    LocalLastChnagedAt = local_last_chnaged_at;
  }

}